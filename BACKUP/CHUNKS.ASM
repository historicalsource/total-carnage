	.FILE	'CHUNKS.ASM'
	.TITLE	'CHUNK/EXPLOSION CODE'
	.WIDTH	132
	.OPTION	B,D,L,T
	.MNOLIST

;
; INITIATED:		 SEPT 2,1990
; MODIFIED:		 !
; SOFTWARE:		 MARK TURMELL
;
; COPYRIGHT (C) 1989 WILLIAMS ELECTRONICS GAMES, INC.
;
;
; GET THE SYSTEM STUFF

	.INCLUDE	"MPROC.EQU"		;MPROC EQUATES
	.INCLUDE	"DISP.EQU"		;DISPLAY PROC. EQUATES
	.INCLUDE	"\VIDEO\SYS\SYS.INC"	;Z UNIT SYSTEM EQUATES
	.INCLUDE	"\VIDEO\SYS\MACROS.HDR"	;MACROS DEFINITIONS
	.INCLUDE	"IMGTBL.GLO"
	.INCLUDE	"GAME.EQU"

;
;SOUNDS EXTERNAL
;
;
;SYMBOLS EXTERNALLY DEFINED
;
	.REF	RANGRAND,GETFPAL,RANDPER,FRANIM,GPALOBJ,GETCPNT
;
;SYMBOLS DEFINED IN THIS FILE
;
	.DEF	BLOOD_LST2,ALL_CHUNKS,CHUNK_OBJ,BEGIN_CHNK,PCTOT
	.DEF	BLOOD_LST1,PCINFO,LASTPC

;
;UNINITIALIZED RAM DEFINITIONS
;
	.BSS	LASTPC,32
	.BSS	PCTOT,16
	.BSS	PCINFO,(32+16+32)*PCSMAX
	.BSS	GENERIC_INIT,7*32-16

;
;EQUATES FOR THIS FILE
;
;FOR STARTING A CHUNK OBJECT
XVAL	.EQU	GENERIC_INIT
YVAL	.EQU	XVAL+32
IMG	.EQU	YVAL+32
ZPOS	.EQU	IMG+32
FLAGS	.EQU	ZPOS+16
ID	.EQU	FLAGS+16
XVEL	.EQU	ID+16
YVEL	.EQU	XVEL+32
;

	.TEXT

CHUNK_OBJ:
;A9=XVEL
;A10=YVEL FOR ADDING IN
;A11=LIST FOR FRANIM, FIRST ENTRY IS IMG TO START, 0 ENDS LIST
;PROCESS FOR CHUNKING CRATE, ETC
;A8=PNTR TO OBJECT TO BLOW UP
;PLACE INTO PCINFO TABLE FOR UPDATING BY THE PC DRIVER (ALL_CHUNKS) PROCESS
	MOVE	@PCTOT,A0
	CMPI	40,A0
	JRGE	DONE
	CALLA	GETCPNT
	MOVX	A1,A0
	SLL	16,A0
;A0=PROPER X,A1=Y
	MOVE	A9,A6
	MOVE	A10,A7
	MOVE	*A11+,A2,L
	MOVI	293,A3			;240
	MOVI	DMAWNZ|M_NOCOLL,A4
	MOVI	CLSDEAD,A5		;I WILL KILL MYSELF
	CALLR	BEGIN_CHNK
	MOVE	*A13(PDATA),A0
	JRZ	NOCH
;CHANGE TO OTHER PAL
	MOVE	A0,*A8(OPAL),W
NOCH
;NOW INSERT THIS PNTR INTO CHUNK DRIVER
	MOVE	@LASTPC,A0,L
	MOVE	A8,*A0+,L
	MOVK	1,A1
	MOVE	A1,*A0+,W		;INIT TICK CNT
	MOVE	*A11+,A9,L		;LIST FOR THIS CHUNK
	MOVE	A9,*A0+,L
	MOVE	A0,@LASTPC,L
	
;WILL NOW GENERATE X CHUNKS FOR CRATE DEATH
AGAIN	MOVE	*A11+,A9,L
	JRZ	DONE
	MOVE	@PCTOT,A2
	INC	A2
	MOVE	A2,@PCTOT
	CALLR	NEWV2			;GET VELOCITIES FOR THIS CHUNK
	CALLR	QUICK_CHNK
	MOVE	*A13(PDATA),A0
	JRZ	NOCH2
;CHANGE TO OTHER PAL
	MOVE	A0,*A8(OPAL),W
NOCH2	MOVE	@LASTPC,A0,L
	MOVE	A8,*A0+,L
	MOVK	1,A1
	MOVE	A1,*A0+,W		;INIT TICK CNT
	MOVE	A9,*A0+,L
	MOVE	A0,@LASTPC,L
	JRUC	AGAIN
DONE	DIE
NEWV	MOVI	-09000H,B0
	MOVI	09000H,B1
	CALLA	RANGRAND
	ADD	A6,A0
	MOVE	A0,@XVEL,L
	MOVI	-07000H,B0
	MOVI	07000H,B1
	CALLA	RANGRAND
	ADD	A7,A0
	MOVE	A0,@YVEL,L
	RETS

NEWV2	MOVI	-0E000H,B0
	MOVI	0E000H,B1
	CALLA	RANGRAND
	ADD	A6,A0
	MOVE	A0,@XVEL,L
	MOVI	-0C000H,B0
	MOVI	0C000H,B1
	CALLA	RANGRAND
	ADD	A7,A0
	MOVE	A0,@YVEL,L
	RETS


ALL_CHUNKS:
;PROCESS WHICH HANDLES ALL CHUNKS
	CLR	A0
	MOVI	PCINFO,A1
	MOVI	PCSMAX,A2
PCL1:	MOVE	A0,*A1+,L
	MOVE	A0,*A1+,W
	MOVE	A0,*A1+,L
	DSJS	A2,PCL1

	MOVI	PCINFO,A0
	MOVE	A0,@LASTPC,L

PCTP:	MOVI	PCINFO,A3
PCTP1:
	MOVE	*A3+,A8,L
	JREQ	DN
;PC PNTR FOUND
	MOVE	*A3,A0,W		;GET SLP TIME
	DEC	A0
	MOVE	A0,*A3+,W
	JRGT	PCOUTA			;JRNE
;READY FOR ANIMATION ON THIS PIECE
	MOVE	*A3,A9,L		;GET FRANIM LIST
	MOVK	4,A1
	JSRP	FRANIM			;PIG ON CYCLES!
	JRC	YDONE			;GET RID OF PIECE
	MOVE	A9,*A3,L
;	SUBK	16,A3
	MOVE	A0,*A3(-16),W		;RESTORE SLEEP TIME
;	ADDK	16,A3
PCOUTA:	
	ADDK	32,A3
	JRUC	PCTP1

YDONE:	SUBI	48,A3
;ONLY NECESSARY IF ONLY PIECE IN LIST?
	CLR	A0
	MOVE	A0,*A3,L
	MOVE	A8,A0
	CALLA	DELOBJ
	MOVE	@PCTOT,A0
	DEC	A0
	MOVE	A0,@PCTOT
	JRNN	YD
	CLR	A0
	MOVE	A0,@PCTOT
YD
	MOVE	@LASTPC,A0,L
	SUBI	80,A0
	MOVE	A0,@LASTPC,L
	MOVE	*A0,A5,L
	JREQ	DN		;BR=ONLY 1 PC IN LIST AND IT DIED!

	MOVE	*A0+,A1,L
	MOVE	*A0+,A2,W
	MOVE	*A0,A5,L
	CLR	A4
	SUBI	48,A0
	MOVE	A4,*A0,L		;ZERO OUT PREVIOUS LAST ENTRY
	MOVE	A1,*A3+,L
	MOVE	A2,*A3+,W
	MOVE	A5,*A3+,L
	JRUC	PCTP1

DN:	SLEEPK	1
	JRUC	PCTP
BEGIN_CHNK:
	MOVE	A0,@XVAL,L
	MOVE	A1,@YVAL,L
	MOVE	A2,@IMG,L
	MOVE	A3,@ZPOS
	MOVE	A4,@FLAGS
	MOVE	A5,@ID
	MOVE	A6,@XVEL,L
	MOVE	A7,@YVEL,L
QUICK_CHNK:
	MOVI	GENERIC_INIT,A14	;DEFINE OBJECT PARAMS
	CALLA	GPALOBJ			;ALLOCATE A COLOR PALETTE
	CALLA	STFOBJ			;STUFF OBJECT DATA
	MOVE	A13,*A0(OPLINK),L
	CALLA	INSOBJ			;INSERT OBJECT INTO LIST
	MOVE	A0,A8
	RETS

*****************************************************************************
;
; FRANIM LISTS OF CHUNKS/BLOOD
;
*****************************************************************************
BLOOD_LST1:
;BLOODY CHUNK
	.LONG	rblot1
	.WORD	NEWPALET|20
	.LONG	RDBOOM	;ONUP1
	.LONG	rblot2
	.WORD	5
	.LONG	rblot3
	.WORD	5
	.LONG	rblot4
	.WORD	5
	.LONG	rblot5
	.WORD	5
	.LONG	rblot6
	.WORD	5
	.LONG	rblot7
	.WORD	5
	.LONG	rblot8
	.WORD	4
	.LONG	rblot9
	.WORD	4
	.LONG	rblot10
	.WORD	5
	.LONG	0
BLOOD_LST2:	
;OVAL BLOOD EXPLOSION
	.LONG	bldclt1
	.WORD	NEWPALET|1
	.LONG	RDBOOM	;ONUP1
	.LONG	bldclt2
	.WORD	6
	.LONG	bldclt3
	.WORD	6
	.LONG	bldclt4
	.WORD	6
	.LONG	bldclt5
	.WORD	6
	.LONG	bldclt6
	.WORD	6
	.LONG	bldclt7
	.WORD	6
	.LONG	bldclt8
	.WORD	7
	.LONG	0
BLOOD_LST3:
;BLOODY CHUNK
	.LONG	rblot1
	.WORD	NEWPALET|40
	.LONG	RDBOOM	;ONUP1
	.LONG	rblot2
	.WORD	5
	.LONG	rblot3
	.WORD	6
	.LONG	rblot4
	.WORD	6
	.LONG	rblot5
	.WORD	5
	.LONG	rblot6
	.WORD	5
	.LONG	rblot7
	.WORD	5
	.LONG	rblot8
	.WORD	4
	.LONG	rblot9
	.WORD	4
	.LONG	rblot10
	.WORD	5
	.LONG	0
*****************************************************************************

	.END

